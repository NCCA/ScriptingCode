proc createSphere(float $minRad, float $maxRad)
{
  polySphere -sx 8 -sy 8 -r `rand $minRad $maxRad`;
  $x = rand(-$minRad,$minRad); 
  $y = rand(-$minRad,$minRad); 
  $z = rand(-$minRad,$minRad); 
  move $x $y $z  ;
  $rx= rand(360);
  $ry= rand(360);
  $rz= rand(360);
  rotate -os $rx $ry $rz;
  $sx= rand($maxRad)+$minRad;
  $sy= rand($maxRad)+$minRad;
  $sz= rand($maxRad)+$minRad;
  scale $sx $sy $sz;
};

proc string createRock(int $numSpheres, float $minRad, float $maxRad)
{
  int $i;
  for ($i = 0; $i < $numSpheres; ++$i) 
  {
    createSphere($minRad,$maxRad);
  }

  string $spheres[] = `ls -type shape "pSphere*"`;
  select $spheres;
  $name= `polyCBoolOp -op 1 -ch 1 -preserveColor 0 -classification 1 -name NewRock`;
  polyTriangulate -ch 1 -name $name;
  polyReduce  -p 50  -replaceOriginal 1 -name $name;
  polySmooth  -name $name;  
  delete -ch $name;
  $sx= rand(360);
  $sy= rand(360);
  $sz= rand(360);
  rotate -os $sx $sy $sz;
  return $name[0];
}

proc createRockCallback() 
{
  // read the control for the number of cubes to be created
  int $numSpheres = `intSliderGrp -q  -v numSpheres`;
  float $minRad = `floatSliderGrp -q  -v minRadius`;
  float $maxRad = `floatSliderGrp -q  -v maxRadius`;
  createRock($numSpheres,  $minRad,  $maxRad);
}

proc createMultipleRocks() 
{
  // read the control for the number of cubes to be created
  int $numRocks = `intSliderGrp -q  -v numRocks`;
  int $numSpheres = `intSliderGrp -q  -v numSpheres`;
  float $minRad = `floatSliderGrp -q  -v minRadius`;
  float $maxRad = `floatSliderGrp -q  -v maxRadius`;
  print("values are " + $numSpheres + " " + $minRad + " " + $maxRad );
  int $i;
  for($i=0; $i<$numRocks; ++$i)
  {  
   $sphereName=createRock($numSpheres,  $minRad,  $maxRad);
  $x = rand(-10,10); 
  $y = 5.0; 
  $z = rand(-10,10); 
  move $x $y $z $sphereName ;
  }
}

proc createRockGUI()
{

  if ( `window -exists RockWindow` ) 
  {
    deleteUI RockWindow; // delete window when it exists
  }
  window -t "rock creation tool" RockWindow;
  columnLayout colLayout;
  floatSliderGrp -p colLayout -field 1 -l "min rock radius" -min 0.2 -max 2.0 -value 0.2 minRadius;
  floatSliderGrp -p colLayout -field 1 -l "max rock radius" -min 0.2 -max 5.0  -value 1.2 maxRadius;

  intSliderGrp -p colLayout -field 1 -l "number of spheres" -min 4 -max 100  -value 4 numSpheres;

  button -p colLayout -l "Create" -command "createRockCallback()";


  intSliderGrp -p colLayout -field 1 -l "number of rocks" -min 4 -max 500  -value 20 numRocks;
  button -p colLayout -l "Create Multiple" -command "createMultipleRocks()";

  showWindow RockWindow;

}

createRockGUI();

